apply plugin: 'com.android.library'

apply from: './VERSIONS.gradle'
def applicationId = getApplicationId()
def playServicesVersion = getPlayServicesVersion()
def supportLibVersion = getSupportLibVersion()
def hasGradle3Support = getGradle3Support();

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }
}

android {
    compileSdkVersion project.ext.getCompileSdkVersion()
    buildToolsVersion project.ext.getBuildToolsVersion()
    publishNonDefault true

    defaultConfig {
        resValue "string", "content_authority", applicationId + '.provider'
        resValue "string", "account_type", applicationId
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        minSdkVersion 15
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            if (hasGradle3Support) {
                java.srcDirs = ['src/main/java', 'src/oreo/java']
            }
        }
    }

    flavorDimensions 'default'

    productFlavors {
        if (!hasGradle3Support) {
            preoreo {
                targetSdkVersion project.ext.getTargetSdkVersion()
                dimension 'default'
            }
            oreo {
                versionNameSuffix 'oreo'
                targetSdkVersion project.ext.getTargetSdkVersion()
                dimension 'default'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20140107'
    compile "com.google.android.gms:play-services-location:$playServicesVersion"
    if (hasGradle3Support) {
        compile "com.android.support:support-compat:26+"
    } else {
        oreoCompile "com.android.support:support-compat:26+"
    }
    compile "com.android.support:appcompat-v7:$supportLibVersion" // override android-permissions
    compile('com.intentfilter:android-permissions:0.1.6') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile 'com.github.tony19:logback-android:1.1.1-9'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.github.jparkie:promise:1.0.3'
}
